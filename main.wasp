app askTheDocuments {
  wasp: {
    version: "^0.11.3"
  },
  title: "PG Vector Example",
  auth: {
    userEntity: User,
    externalAuthEntity: SocialLogin,
    methods: {
      google: {
        getUserFieldsFn: import { getUserFields } from "@server/auth/google.js",
        configFn: import { getConfig } from "@server/auth/google.js",
      }
    },
    onAuthFailedRedirectTo: "/"
  },
  dependencies: [
    ("openai", "^4.5.0"),
    ("react-hook-form", "^7.45.4"),
    ("@nextui-org/react", "^2.1.10"),
    ("framer-motion", "^10.16.4"),
    ("pgvector", "0.1.5"),
    ("cheerio", "1.0.0-rc.12"),
    ("axios", "^1.4.0"),
    ("react-markdown", "^8.0.7"),
    ("node-html-markdown", "1.3.0")
  ],
  client: {
    rootComponent: import { Layout } from "@client/Layout.tsx",
  },
  db: {
    system: PostgreSQL,
    prisma: {
      clientPreviewFeatures: ["postgresqlExtensions"],
      dbExtensions: [{
        name: "pgvector",
        map: "vector"
      }]
    }
  }
}

entity User {=psl
  id                        Int           @id @default(autoincrement())
  email                     String?
  externalAuthAssociations  SocialLogin[]
psl=}

entity SocialLogin {=psl
  id          Int       @id @default(autoincrement())
  provider    String
  providerId  String
  user        User      @relation(fields: [userId], references: [id], onDelete: Cascade)
  userId      Int
  createdAt   DateTime  @default(now())
  @@unique([provider, providerId, userId])
psl=}

entity Document {=psl
  id String @id @default(uuid())
  title String
  url String @unique
  content String
  embedding Unsupported("vector(1536)")
  createdAt DateTime @default(now())
  updatedAt DateTime @updatedAt
psl=}

route RootRoute { path: "/", to: MainPage }
page MainPage {
  component: import { Main } from "@client/pages/MainPage.tsx"
}

action embedDocument {
  fn: import { embedDocument } from "@server/documents.js",
  entities: [Document],
}

action getScrapeCandidates {
  fn: import { getScrapeCandidates } from "@server/documents.js",
  entities: [Document],
}

query getDocuments {
  fn: import { getDocuments } from "@server/documents.js",
  entities: [Document]
}

action searchDocuments {
  fn: import { searchDocuments } from "@server/documents.js",
  entities: [Document]
}

action askDocuments {
  fn: import { askDocuments } from "@server/documents.js",
  entities: [Document]
}

action deleteDocument {
  fn: import { deleteDocument } from "@server/documents.js",
  entities: [Document]
}
